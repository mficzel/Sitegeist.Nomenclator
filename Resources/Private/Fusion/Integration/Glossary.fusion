prototype(Sitegeist.Nomenclator:Glossary) < prototype(Neos.Neos:ContentComponent) {
    headline = Neos.Neos:Editable {
        node = ${entryItem}
        property = 'headline'
        block = false
    }

    entries = Neos.Fusion:Map {
        items = ${q(node).children('[instanceof Sitegeist.Nomenclator:Glossary.Entry]').get()}
        itemName = 'entryItem'
        itemRenderer = Neos.Fusion:DataStructure {
            __node = ${entryItem}
            title = Neos.Neos:Editable {
                node = ${entryItem}
                property = 'title'
                block = false
            }
            description = Neos.Neos:Editable {
                node = ${entryItem}
                property = 'description'
                block = false
            }
            variants = Neos.Neos:Editable {
                @if.inBackend = ${node.context.inBackend}
                node = ${entryItem}
                property = 'variants'
                block = false
            }
            imageSource = Neos.Neos:ImageUri {
                asset = ${q(entryItem).property('image')}
            }
            imageAlt = ${q(entryItem).property('imageAlternativeText')}
            imageTitle = ${q(entryItem).property('imageTitle')}

        }
    }

    renderer = Sitegeist.Nomenclator:Presentation.Glossary {
        headline = ${props.headline}
        entries = ${props.entries}
    }
    prototype(Sitegeist.Nomenclator:Presentation.Glossary.Entry) {
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @if.hasNode = ${item.__node}
            @if.inBackend = ${documentNode.context.inBackend}
            node = ${item.__node}
        }
    }
}
