prototype(Sitegeist.Nomenclator:Content.Glossary) < prototype(Neos.Neos:ContentComponent) {
    headline = Neos.Neos:Editable {
        property = 'headline'
        block = false
    }

    entries = Neos.Fusion:Map {
        items = ${q(node).children('[instanceof Sitegeist.Nomenclator:Content.Glossary.Entry]').get()}
        itemName = 'entryItem'
        itemRenderer = Neos.Fusion:DataStructure {
            __node = ${entryItem}
            title = Neos.Neos:Editable {
                node = ${entryItem}
                property = 'title'
                block = false
            }
            shortDescription = Neos.Neos:Editable {
                @if.inBackend = ${node.context.inBackend}
                node = ${entryItem}
                property = 'shortDescription'
                block = false
            }

            longDescription = Neos.Neos:Editable {
                node = ${entryItem}
                property = 'longDescription'
                block = false
            }

            variants = Neos.Neos:Editable {
                @if.inBackend = ${node.context.inBackend}
                node = ${entryItem}
                property = 'variants'
                block = false
            }
            imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
                asset = ${q(entryItem).property('image')}
            }
            srcset = "300w"
            sizes = "100vw"
            imageAlt = ${q(entryItem).property('imageAlternativeText')}
            imageTitle = ${q(entryItem).property('imageTitle')}

        }
    }

    renderer = Sitegeist.Nomenclator:Component.Glossary {
        headline = ${props.headline}
        entries = ${props.entries}
    }
    prototype(Sitegeist.Nomenclator:Component.Glossary.Entry) {
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @if.hasNode = ${item.__node}
            @if.inBackend = ${documentNode.context.inBackend}
            node = ${item.__node}
        }
    }
}
